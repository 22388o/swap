/**
 * Vapor smart contract -> Equity language
 *
 * @title Hash Time Lock Contract (HTLC)
 *
 * @author Meheret Tesfaye Batu <meherett@zoho.com>
 *
 * A Hash Time Lock Contract is essentially a type of payment in which two
 * people agree to a financial arrangement where one party will pay the other
 * party a certain amount of cryptocurrency, such as Bitcoin or Vapor assets.
 * However, because these contracts are Time Locked, the receiving party only
 * has a certain amount of time to accept the payment, otherwise the money
 * can be returned to the sender.
 */

contract HTLC (
  secret_hash: Hash,
  recipient: PublicKey,
  sender: PublicKey,
  sequence: Integer
) locks valueAmount of valueAsset {
  clause claim(preimage: String, sig: Signature) {
    verify sha256(preimage) == secret_hash
    verify checkTxSig(recipient, sig)
    unlock valueAmount of valueAsset
  }
  clause refund(sig: Signature) {
    verify above(sequence)
    verify checkTxSig(sender, sig)
    unlock valueAmount of valueAsset
  }
}